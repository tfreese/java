import org.w3c.dom.Document

import javax.xml.XMLConstants
import javax.xml.parsers.DocumentBuilder
import javax.xml.parsers.DocumentBuilderFactory
import javax.xml.transform.TransformerFactory
import javax.xml.transform.dom.DOMSource
import javax.xml.transform.stream.StreamResult
import javax.xml.transform.stream.StreamSource

plugins {
    id "java"
    id "org.openjfx.javafxplugin"
}

description = "Alles zum ausprobieren"

//configurations.configureEach {
//    exclude group: "ch.qos.logback", module: "logback-classic"
//}

configurations {
    jaxb
}

sourceSets {
    main {
        java {
            srcDir(layout.buildDirectory.get().dir("generated").dir("sources").dir("jaxb"))
        }
    }
}

dependencies {
    jaxb "com.sun.xml.bind:jaxb-xjc"
    jaxb "com.sun.xml.bind:jaxb-impl"

    javafx {
        version = "$version_javafx"
        modules = ["javafx.controls"]
    }

    implementation("com.fasterxml.jackson.dataformat:jackson-dataformat-xml")
    implementation("com.fasterxml.jackson.module:jackson-module-jaxb-annotations")
    implementation("com.h2database:h2")
    implementation("com.lmax:disruptor")
    implementation("com.zaxxer:HikariCP")
    implementation("dev.failsafe:failsafe")
    implementation("io.projectreactor:reactor-test")
    implementation("jakarta.json.bind:jakarta.json.bind-api")
    implementation("org.apache.commons:commons-compress")
    implementation("org.apache.commons:commons-lang3")
    implementation("org.apache.logging.log4j:log4j-to-slf4j")
    implementation("org.apache.lucene:lucene-analyzers-common")
    implementation("org.apache.xmlgraphics:batik-codec")
    implementation("org.apache.xmlgraphics:batik-swing")
    implementation("org.apache.xmlgraphics:batik-transcoder")
    implementation("org.apache.velocity:velocity-engine-core")
    implementation("org.codehaus.plexus:plexus-cipher")
    implementation("org.codehaus.plexus:plexus-container-default")
    implementation("org.freemarker:freemarker")
    implementation("org:jaudiotagger")
    implementation("org.hsqldb:hsqldb")
    implementation("org.jsoup:jsoup")
    implementation("org.mariadb.jdbc:mariadb-java-client")
    implementation("org.springframework.boot:spring-boot-starter-mail") {
        exclude group: "ch.qos.logback", module: "logback-classic"
        exclude group: "org.apache.logging.log4j", module: "log4j-to-slf4j"
    }

    runtimeOnly("com.oracle.database.jdbc:ojdbc11")
    runtimeOnly("org.glassfish.jaxb:jaxb-runtime") // Implementierung von jakarta.json.bind:jakarta.json.bind-api
    runtimeOnly("org.slf4j:slf4j-simple")

    // tomcat-juli ist in tomcat-catalina enthalten, aber auch viele andere Jars die nicht ben√∂tigt werden.
    testImplementation("org.apache.tomcat:tomcat-catalina") {
        exclude group: "org.apache.tomcat"
    }
    testImplementation("org.apache.tomcat:tomcat-juli")

    testImplementation("org.mockito:mockito-junit-jupiter")
}

tasks.register("xslt") {
    group = "My Tasks"

//    dependsOn("compileJava")

    doLast {
        Directory destFolder = layout.buildDirectory.get().dir("classes").dir("java").dir("main").dir("xslt")
        mkdir destFolder

        Directory srcFolder = layout.projectDirectory.dir("src").dir("xslt")

        try (InputStream xsl = srcFolder.file("article.xsl").getAsFile().newInputStream()
             InputStream xml = srcFolder.file("article.xml").getAsFile().newInputStream()
             OutputStream html = destFolder.file("article.html").getAsFile().newOutputStream()) {
            DocumentBuilderFactory documentBuilderFactory = DocumentBuilderFactory.newInstance()
            // documentBuilderFactory.setNamespaceAware(true);
            // documentBuilderFactory.setValidating(true);
            documentBuilderFactory.setAttribute(XMLConstants.ACCESS_EXTERNAL_DTD, "")
            documentBuilderFactory.setAttribute(XMLConstants.ACCESS_EXTERNAL_SCHEMA, "")

            DocumentBuilder documentBuilder = documentBuilderFactory.newDocumentBuilder()
            Document document = documentBuilder.parse(xml)

            TransformerFactory transformerFactory = TransformerFactory.newInstance()
            transformerFactory.setAttribute(XMLConstants.ACCESS_EXTERNAL_DTD, "")
            transformerFactory.setAttribute(XMLConstants.ACCESS_EXTERNAL_STYLESHEET, "")

            javax.xml.transform.Transformer transformer = transformerFactory.newTransformer(new StreamSource(xsl))
            transformer.transform(new DOMSource(document), new StreamResult(html))

            html.flush()
        }
    }

//    def xslt = new File("../../d99qaia-java/src/main/resources/xslt/article.xsl").getText()
//    def xml = new File("../../d99qaia-java/src/main/resources/xslt/article.xml").getText()
//
//    def transformer = TransformerFactory.newInstance().newTransformer(new StreamSource(new StringReader(xslt)))
//
//    def html = new FileOutputStream("build/article.html")
//
//    transformer.transform(new StreamSource(new StringReader(xml)), new StreamResult(html))
}

tasks.register("genJaxb") {
    group = "My Tasks"

    doLast {
        Directory sourcesDir = layout.buildDirectory.get().dir("generated").dir("sources").dir("jaxb")
        Directory resourcesDir = layout.projectDirectory.dir("schemas")
        RegularFile binding = layout.projectDirectory.file("schema.xjb")

        delete sourcesDir
        mkdir sourcesDir

        ant {
            taskdef name: "xjc", classname: "com.sun.tools.xjc.XJCTask", classpath: configurations.jaxb.asPath

            xjc(destdir: sourcesDir,
                    schema: resourcesDir.dir("GolfCountryClub").file("GolfCountryClub.xsd"),
                    binding: binding,
                    package: "de.freese.xjc.golfcountryclub",
                    encoding: "UTF-8",
                    readonly: true,
                    extension: true) {
                arg(value: "-npa")
            }

            xjc(destdir: sourcesDir,
                    schema: resourcesDir.dir("PhoneBanking").file("PhoneBanking.xsd"),
                    binding: binding,
                    package: "de.freese.xjc.phonebanking",
                    encoding: "UTF-8",
                    readonly: true,
                    extension: true) {
                arg(value: "-npa")
            }

            xjc(destdir: sourcesDir,
                    schema: resourcesDir.dir("PublicationCatalogue").file("Catalogue.xsd"),
                    binding: binding,
                    package: "de.freese.xjc.catalogue",
                    encoding: "UTF-8",
                    readonly: true,
                    extension: true) {
                arg(value: "-npa")
            }

            xjc(destdir: sourcesDir,
                    schema: resourcesDir.dir("SpaceWarGame").file("SpaceWarGame.xsd"),
                    binding: binding,
                    package: "de.freese.xjc.spacewargame",
                    encoding: "UTF-8",
                    readonly: true,
                    extension: true) {
                arg(value: "-npa")
            }
        }
    }
}

compileJava.dependsOn("xslt", "genJaxb")
