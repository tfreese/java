// Execute Tasks in SubModule: gradle MODUL:clean build
plugins {
    id "de.freese.gradle.java.conventions" version "$version_myJavaConventionPlugin" apply false
    id "de.freese.gradle.sonarlint-conventions" version "$version_myJavaConventionPlugin" apply false

    id "io.spring.dependency-management" version "$version_springDependencyManagement" apply false
    id "org.openjfx.javafxplugin" version "$version_javafxPlugin" apply false
    id "org.springframework.boot" version "$version_springBoot" apply false
    id "name.remal.sonarlint" version "$version_nameRemalSonarlintPlugin" apply false
}

allprojects {
    apply plugin: "base"

    clean {
        doFirst {
            def dirs = ["bin", "logs", "out", "target"]

            dirs.each(dir -> {
                Directory directory = layout.projectDirectory.dir(dir)

                if (directory.getAsFile().exists()) {
                    // println("delete: " + directory)
                    logger.lifecycle("delete: {}", directory)
                    // delete fileTree(directory).include("**/*")
                    delete directory
                }
            })
        }
    }

    ant.lifecycleLogLevel = "INFO"

    // repositories {
    //
    // }
}

subprojects {
    apply plugin: "de.freese.gradle.java.conventions"
    apply plugin: "io.spring.dependency-management"
    apply plugin: "name.remal.sonarlint"
    apply plugin: "de.freese.gradle.sonarlint-conventions"

    dependencyManagement {
        imports {
            //mavenBom(org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES)
            mavenBom("org.springframework.boot:spring-boot-dependencies:$version_springBoot")
        }

        dependencies {
            dependency("ch.qos.logback.db:logback-classic-db:$version_logbackClassicDb")
            dependency("com.danielflower.apprunner:javasysmon:$version_javasysmon")
            dependency("com.lmax:disruptor:$version_disruptor")
            dependency("dev.failsafe:failsafe:$version_failsafe")
            dependency("org.apache.commons:commons-compress:$version_commonsCompress")

            dependency("org.apache.lucene:lucene-queryparser:$version_lucene")
            // dependency("org.apache.lucene:version_luceneAnalysisCommon:$version_luceneAnalysisCommon")

            dependencySet(group: "org.apache.lucene", version: "$version_lucene") {
                entry "lucene-analysis-common"
                entry "lucene-queryparser"
            }

            dependencySet(group: "org.apache.maven", version: "$version_maven") {
                entry "maven-model-builder"
                entry "maven-settings-builder"
            }

            dependencySet(group: "org.apache.tomcat", version: dependencyManagement.importedProperties["tomcat.version"]) {
                entry "tomcat-catalina"
                entry "tomcat-juli"
            }

            dependencySet(group: "org.apache.xmlgraphics", version: "$version_batik") {
                entry "batik-codec"
                entry "batik-swing"
            }

            dependency("org:jaudiotagger:$version_jaudiotagger")
            dependency("org.jsoup:jsoup:$version_jsoup")

            dependency("org.jfree:jfreechart:$version_jfreechart") {
                // exclude "com.lowagie:itext"
                // exclude "xml-apis:xml-apis"
            }

            dependency("org.apache.velocity:velocity-engine-core:$version_velocity")
        }
    }

    pluginManager.withPlugin("java") {
        dependencies {
            //implementation(platform("org.springframework.boot:spring-boot-dependencies:$version_springBoot"))

            testImplementation("org.assertj:assertj-core")
            testImplementation("org.awaitility:awaitility")
            testImplementation("org.junit.jupiter:junit-jupiter")
            testImplementation("org.mockito:mockito-junit-jupiter")

            testRuntimeOnly("org.junit.platform:junit-platform-launcher")
        }
    }
}

// For Composite Build.
tasks.register("cleanAll", GradleBuild) {
    tasks = ["clean"]
}
tasks.register("buildAll", GradleBuild) {
    tasks = ["build"]
}
